/*
 * the SDF MoCC Library
 * @author: Julien Deantoni
 * date : Sun September 04 2011  15:03:42 CEST 
 */

Library SDF{

	imports{ 
		import "platform:/plugin/fr.inria.aoste.timesquare.ccslkernel.model/ccsllibrary/kernel.ccslLib" as kernel;
		import "platform:/plugin/fr.inria.aoste.timesquare.ccslkernel.model/ccsllibrary/CCSL.ccslLib" as kernel;
	}

				
	RelationLibrary SDF_Relations{
		/*
		 * Constraint associated with the token manipulation
		 */
		RelationDeclaration Token(Token_write:clock, Token_read:clock, Token_delay:int)
		ConditionalRelationDefinition TokenDef[Token]{
			Integer zero=0
			
			Expression Token_readDelayedForDelay=DelayFor( DelayForClockToDelay-> Token_read,
													 DelayForClockForCounting-> Token_read,
													 DelayForDelay-> Token_delay
			)
			
			switch{
				case IntInf{
					leftValue IntegerVariableRef[Token_delay]
					rightValue IntegerRef[one]
					}:
					Relation Token_causalityImmediate[Causes](LeftClock-> Token_write, RightClock-> Token_read)
					
					case IntSup{
					leftValue IntegerVariableRef[Token_delay]
					rightValue IntegerRef[zero]
					}:
					Relation Token_causalityImmediate[Causes](LeftClock-> Token_write, RightClock-> Token_readDelayedForDelay)
				}default Relation Token_causalityDelayed[Coincides](Clock1-> Token_write, Clock2-> Token_write)
		}
				
		
		/*
		 * Constraints associated to each input
		 */
		RelationDeclaration Input(Input_actor:clock, Input_read:clock, Input_weight:int)
		RelationDefinition InputDef[Input]{
			Sequence Input_ByWeight=(IntegerVariableRef[Input_weight])
			Expression Input_readByWeight=FilterBy( FilterByClock->Input_read,
													FilterBySeq-> Input_ByWeight
			)
			Relation Input_weightTokenCausesExec[Causes](LeftClock-> Input_readByWeight,
														 RightClock-> Input_actor
			)
		}
		
		/*
		 * Constraints associated to each output
		 */
		RelationDeclaration Output(Output_actor:clock, Output_write:clock, Output_weight:int)
		RelationDefinition OutputDef[Output]{
			Sequence Output_ByWeight= 1 (IntegerVariableRef[Output_weight])
			Expression Output_writeByWeight=FilterBy( FilterByClock->Output_write,
													FilterBySeq-> Output_ByWeight
			)
			Relation Output_ExecWriteFirstToken[Coincides](Clock1-> Output_actor,
														   Clock2-> Output_writeByWeight
			)
		}
		
		/*
		 * The arc relation is combining the other and can be the only one used to apply the MoCC 
		 */
		RelationDeclaration Arc(Arc_delay:int, 
								Arc_source:clock,
								Arc_outWeight:int,
								Arc_target:clock,
								Arc_inWeight:int
		)
		RelationDefinition ArcDef[Arc]{
			//internal clocks
			Clock Arc_Write:clock 
			Clock Arc_read:clock
			Relation Arc_output[Output](Output_actor-> Arc_source,
										Output_write-> Arc_Write,
										Output_weight-> Arc_outWeight
			)
			Relation Arc_token[Token](Token_write-> Arc_Write,
									   Token_read-> Arc_read,
									   Token_delay-> Arc_delay
			)
			Relation Arc_input[Input](Input_actor-> Arc_target,
										Input_read-> Arc_read,
										Input_weight-> Arc_inWeight
			)



		}
	}
	
	

}